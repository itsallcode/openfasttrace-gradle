plugins {
    id 'java-gradle-plugin'
    id 'jacoco'
    id 'com.gradle.plugin-publish' version '0.15.0'
    id 'com.github.hierynomus.license' version '0.16.1'
    id 'org.sonarqube' version '3.3'
    id 'pl.droidsonroids.jacoco.testkit' version '1.0.8'
}

repositories {
    mavenCentral()
}

apply from: 'gradle/workAroundJacocoGradleTestKitIssueOnWindows.gradle'

version = '0.9.0'
group = 'org.itsallcode'
sourceCompatibility = 11
targetCompatibility = 11

ext {
    gradlePluginId = 'org.itsallcode.openfasttrace'
    oftVersion = '3.2.0'
    junitVersion = '5.7.2'
    if (project.hasProperty('oftSourceDir')) {
        oftSourceDir = file(project.oftSourceDir)
        useOftSources = oftSourceDir.exists()
        if (!useOftSources) {
            logger.warn "OFT source directory $oftSourceDir does not exist"
        }
    } else {
        oftSourceDir = 'oft-dir-not-found'
        useOftSources = false
    }
}

def compiledClassesFolders(File dir) {
    def classDirs = new HashSet()
    file(dir).eachFileRecurse(groovy.io.FileType.DIRECTORIES) {
        String normalizedName = it.toString().replace('\\', '/')
        if (normalizedName.endsWith('/target/classes') && !normalizedName.contains('testutil')) {
            classDirs.add(it)
        }
    }
    return classDirs
}

dependencies {
    if (useOftSources) {
        logger.lifecycle "Including OpenFastTrace sources from Maven target dir $oftSourceDir"
        implementation files(compiledClassesFolders(oftSourceDir)) {
            builtBy "compileOft"
        }
    } else {
        implementation "org.itsallcode.openfasttrace:openfasttrace-api:$oftVersion"
        implementation "org.itsallcode.openfasttrace:openfasttrace-core:$oftVersion"
        implementation "org.itsallcode.openfasttrace:openfasttrace-exporter-specobject:$oftVersion"
        runtimeOnly "org.itsallcode.openfasttrace:openfasttrace:$oftVersion"
    }
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
    testImplementation "org.hamcrest:hamcrest-core:2.2"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

task compileOft(type: Exec) {
    workingDir oftSourceDir
    if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
        commandLine 'mvn.cmd', 'resources:resources', 'compiler:compile'
    } else {
        commandLine "mvn", 'resources:resources', 'compiler:compile'
    }
}

clean {
    def exampleProjects = rootProject.file('example-projects').listFiles()
    def propertyFiles = exampleProjects.collect { new File(it, 'gradle.properties') }
    propertyFiles.each { delete it }
}

gradlePlugin {
    plugins {
        openFastTracePlugin {
            id = gradlePluginId
            implementationClass = 'org.itsallcode.openfasttrace.gradle.OpenFastTracePlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/itsallcode/openfasttrace-gradle'
    vcsUrl = 'https://github.com/itsallcode/openfasttrace-gradle'
    description = 'Gradle plugin for tracing requirements using OpenFastTrace'
    tags = ['requirementstracing', 'requirements', 'tracing', 'reqtracing', 'openfasttrace', 'oft']

    plugins {
        openFastTracePlugin {
            id = gradlePluginId
            displayName = 'OpenFastTrace requirements tracing plugin'
        }
    }
}

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = logger.isEnabled(LogLevel.INFO)
    }
    finalizedBy jacocoTestReport
}

test.onlyIf { rootProject.name == 'openfasttrace-gradle' }

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled true
    }
}

project.tasks["sonarqube"].dependsOn jacocoTestReport

publishPlugins.dependsOn check

license {
    header = file('gradle/license-header.txt')
    strictCheck = true
    skipExistingHeaders = false
    ext.year = 2017
    ext.name = "It's all code"
    ext.email = 'christoph at users.sourceforge.net'
}

downloadLicenses {
    includeProjectDependencies = true
    ignoreFatalParseErrors = false
}
