plugins {
    id 'java-gradle-plugin'
    id 'jacoco'
    id 'signing'
    id 'com.gradle.plugin-publish' version '1.2.1'
    id 'org.sonarqube' version '4.4.1.3373'
    id 'pl.droidsonroids.jacoco.testkit' version '1.0.12'
    id 'com.github.ben-manes.versions' version '0.50.0'
    id 'org.sonatype.gradle.plugins.scan' version '2.7.0'
}

repositories {
    mavenCentral()
}

apply from: 'gradle/workAroundJacocoGradleTestKitIssueOnWindows.gradle'

version = '1.1.0'
group = 'org.itsallcode'

ext {
    gradlePluginId = 'org.itsallcode.openfasttrace'
    oftVersion = '3.7.1'
    junitVersion = '5.10.1'
    if (project.hasProperty('oftSourceDir')) {
        oftSourceDir = file(project.oftSourceDir)
        useOftSources = oftSourceDir.exists()
        if (!useOftSources) {
            logger.warn "OFT source directory $oftSourceDir does not exist"
        }
    } else {
        oftSourceDir = 'oft-dir-not-found'
        useOftSources = false
    }
}

def compiledClassesFolders(File dir) {
    def classDirs = new HashSet()
    file(dir).eachFileRecurse(groovy.io.FileType.DIRECTORIES) {
        String normalizedName = it.toString().replace('\\', '/')
        if (normalizedName.endsWith('/target/classes') && !normalizedName.contains('testutil')) {
            classDirs.add(it)
        }
    }
    return classDirs
}

dependencies {
    if (useOftSources) {
        logger.lifecycle "Including OpenFastTrace sources from Maven target dir $oftSourceDir"
        implementation files(compiledClassesFolders(oftSourceDir)) {
            builtBy "compileOft"
        }
    } else {
        implementation "org.itsallcode.openfasttrace:openfasttrace-api:$oftVersion"
        implementation "org.itsallcode.openfasttrace:openfasttrace-core:$oftVersion"
        implementation "org.itsallcode.openfasttrace:openfasttrace-exporter-specobject:$oftVersion"
        runtimeOnly "org.itsallcode.openfasttrace:openfasttrace:$oftVersion"
    }
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
    testImplementation "org.hamcrest:hamcrest-core:2.2"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

java {
    toolchain {
        def javaVersion = project.hasProperty('javaVersion') ? project.getProperty('javaVersion') : 11
        languageVersion = JavaLanguageVersion.of(javaVersion)
    }
    modularity.inferModulePath = false
}

javadoc {
    failOnError = true
    options.addBooleanOption('html5', true)
    options.addStringOption('Xwerror', '-quiet')
}

task compileOft(type: Exec) {
    workingDir oftSourceDir
    if (System.getProperty('os.name').toLowerCase(Locale.ROOT).contains('windows')) {
        commandLine 'mvn.cmd', 'resources:resources', 'compiler:compile'
    } else {
        commandLine "mvn", 'resources:resources', 'compiler:compile'
    }
}

clean {
    def exampleProjects = rootProject.file('example-projects').listFiles()
    def propertyFiles = exampleProjects.collect { new File(it, 'gradle.properties') }
    propertyFiles.each { delete it }
}

gradlePlugin {
    website = 'https://github.com/itsallcode/openfasttrace-gradle'
    vcsUrl = 'https://github.com/itsallcode/openfasttrace-gradle.git'
    plugins {
        openFastTracePlugin {
            id = gradlePluginId
            implementationClass = 'org.itsallcode.openfasttrace.gradle.OpenFastTracePlugin'
            displayName = 'OpenFastTrace requirements tracing plugin'
            description = 'Gradle plugin for tracing requirements using OpenFastTrace'
            tags.addAll(['requirementstracing', 'requirements', 'tracing', 'reqtracing', 'openfasttrace', 'oft'])
        }
    }
}

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = logger.isEnabled(LogLevel.INFO)
    }
    finalizedBy jacocoTestReport
}

test.onlyIf { rootProject.name == 'openfasttrace-gradle' }

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
    }
}

project.tasks["sonar"].dependsOn jacocoTestReport

sonarqube {
    properties {
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.organization", "itsallcode"
    }
}

publishPlugins.dependsOn check

def isNonStable = { String version ->
  def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
  def regex = /^[0-9,.v-]+(-r)?$/
  return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
  gradleReleaseChannel = "current"
  rejectVersionIf {
    isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
  }
}

ossIndexAudit {
  allConfigurations = false
  useCache = true
  excludeVulnerabilityIds = []
  printBanner = false
}

rootProject.tasks["build"].dependsOn(tasks["ossIndexAudit"])
